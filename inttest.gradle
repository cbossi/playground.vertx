/**
 * Create and configure a separate source set for integration tests:
 * https://www.michael-bull.com/blog/2016/06/04/separating-integration-and-unit-tests-with-gradle
 */

/**
 * Required in order to access the project's external dependencies.
 */
configurations {
  inttestImplementation.extendsFrom testImplementation
  inttestRuntime.extendsFrom testRuntime
  inttestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
  inttest {
    java.srcDir file('src/inttest/java')
    resources.srcDir file('src/inttest/resources')

    /**
     * Required to access classes from the main and test source sets.
     */
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

dependencies {
  /**
   * Sharing flyway version with the flyway gradle plugin is difficult, since variables cannot be used in plugins DSL:
   * https://stackoverflow.com/a/37749402/6231489
   * There would be a workaround, but a quite ugly/complex one:
   * https://github.com/gradle/gradle/issues/1697#issuecomment-386824663
   */
  inttestImplementation "org.flywaydb:flyway-core:5.1.4"
}

task intTest(type: Test) {
  useJUnitPlatform()

  group = LifecycleBasePlugin.VERIFICATION_GROUP
  description = 'Runs the integration tests.'

  testClassesDirs = sourceSets.inttest.output.classesDirs
  classpath = sourceSets.inttest.runtimeClasspath

  mustRunAfter tasks.test
}

check.dependsOn intTest


/**
 * IntelliJ Setup
 */
apply plugin: 'idea'

idea {
  module {
    configurations.inttestImplementation.canBeResolved = true

    testSourceDirs += project.sourceSets.inttest.java.srcDirs
    testSourceDirs += project.sourceSets.inttest.resources.srcDirs
    scopes.TEST.plus += [configurations.inttestImplementation]
  }
}
